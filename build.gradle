/**
 *******************************************************************************
 * Copyright 2019-present Jay B. <j@1791.io>                                   *
 *                                                                             *
 * Licensed under the Apache License, Version 2.0 (the "License");             *
 * you may not use this file except in compliance with the License.            *
 * You may obtain a copy of the License at                                     *
 *                                                                             *
 *     http://www.apache.org/licenses/LICENSE-2.0                              *
 *                                                                             *
 * Unless required by applicable law or agreed to in writing, software         *
 * distributed under the License is distributed on an "AS IS" BASIS,           *
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.    *
 * See the License for the specific language governing permissions and         *
 * limitations under the License.                                              *
 *******************************************************************************
 */

import groovy.text.SimpleTemplateEngine

plugins {
  // Java plugin [ https://docs.gradle.org/5.6.2/userguide/java_plugin.html ].
  id 'java'

  // Java Library plugin [ https://docs.gradle.org/5.6.2/userguide/java_library_plugin.html ].
  id 'java-library'

  // Kotlin JVM plugin [ https://plugins.gradle.org/plugin/org.jetbrains.kotlin.jvm ].
  id 'org.jetbrains.kotlin.jvm' version '1.3.50'

  // Eclipse plugin [ https://docs.gradle.org/5.6.2/userguide/eclipse_plugin.html ].
  id 'eclipse'
}

repositories {
  // By default, resolve dependencies using Bintrayâ€™s JCenter repository [ https://jcenter.bintray.com ].
  jcenter()
}

dependencies {
  // The Kotlin Standard Library [ https://github.com/JetBrains/kotlin/tree/v1.3.50/libraries/stdlib/jdk8 ].
  implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.50'

  // The Java Native Access library [ https://github.com/java-native-access/jna/tree/5.4.0 ].
  implementation 'net.java.dev.jna:jna:5.4.0'

  // The Google Guava library [ https://github.com/google/guava/tree/v28.1 ].
  implementation 'com.google.guava:guava:28.1-jre'

  // The JUnit Jupiter libraries [ https://github.com/junit-team/junit5/tree/r5.5.2 ].
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
}

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

group = 'io.seventeenninetyone'

version = {
  File versionFile = file 'VERSION'
  String versionText = versionFile.getText 'UTF-8'
  versionText.trim()
}()

sourceSets {
  main.java.srcDirs = [
    './src/main/java',
    './src/main/kotlin'
  ]
  test.java.srcDirs = [
    './src/test/java',
    './src/test/kotlin'
  ]

  main.kotlin.srcDirs = [
    './src/main/kotlin',
    './src/main/java'
  ]
  test.kotlin.srcDirs = [
    './src/test/kotlin',
    './src/test/java'
  ]
}

test {
  // Use JUnit Platform (AKA JUnit 5) to execute the tests [ https://docs.gradle.org/5.6.2/javadoc/org/gradle/api/tasks/testing/Test.html#useJUnitPlatform-- ].
  useJUnitPlatform()
}

task generateRuntimeProjectFile {
  def templateFile = file 'src/main/kotlin/io/seventeenninetyone/Project.kt.txt'
  def templateText = templateFile.getText 'UTF-8'
  Map<String, Object> templateBinding = [ 'carlieVersion': "${project.version}" ]
  def templateEngine = new SimpleTemplateEngine()
  def template = templateEngine.createTemplate templateText
  def processedTemplate = template.make templateBinding
  String projectFileContents = "${processedTemplate}"
  def projectFile = file 'src/main/kotlin/io/seventeenninetyone/Project.kt'
  projectFile.setText projectFileContents, 'UTF-8'
}

tasks.compileKotlin.dependsOn generateRuntimeProjectFile

wrapper {
  distributionType = Wrapper.DistributionType.ALL
  gradleVersion = '5.6.2'
}
